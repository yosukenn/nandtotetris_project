// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    static int maped;

    /** Initializes the keyboard. */
    function void init() {
        let maped = 24576;
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        var char pressed;
        let pressed = Memory.peek(maped);
        if (pressed = 0) {
            return 0;
        }
        else {
            return pressed;
        }
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char pressed;
        // TODO print cursor ?

        while (Keyboard.keyPressed() = 0) {
            // do nothing
        }
        let pressed = Keyboard.keyPressed();

        while (~(Keyboard.keyPressed() = 0)) {
            // do nothing
        }
        do Output.printChar(pressed);
        return pressed;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String s;
        var char c;
        var boolean repeat;
        do Output.printString(message);
        let s = String.new(100);

        let repeat = true;
        while (repeat) {
            let c = Keyboard.readChar();
            if (c = 128) { // newLine
                do Output.println();
                let repeat = false;
            }
            else { // backSpace
                if (c = 129) {
                    do s.eraseLastChar();
                    do Output.backSpace();
                }
                else {
                    let s = s.appendChar(c);
                }
            }
        }
        return s;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String s;
        var int i;
        var boolean repeat;
        do Output.printString(message);
        let s = String.new(100);

        let repeat = true;
        while (repeat) {
            let i = Keyboard.readChar();
            if (i = 128) { // newLine
                do Output.println();
                let repeat = false;
            }
            else { // backSpace
                if (i = 129) {
                    do s.eraseLastChar();
                    do Output.backSpace();
                }
                else {
                    let s = s.appendChar(i);
                }
            }
        }
        return s.intValue();
    }   
}
